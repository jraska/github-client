plugins {
  id "com.jraska.module.graph.assertion" version "1.4.0"
  id "com.github.triplet.play" version "3.0.0"
  id "com.jraska.github.client.firebase"
  id 'com.jraska.gradle.buildtime'
  id 'com.jraska.github.client.release'
  id 'com.jraska.module.stats'
  id 'com.jraska.module.lint.report'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
if (project.hasProperty("enable-performance-plugin")) {
  apply plugin: 'com.google.firebase.firebase-perf'
}

apply plugin: 'com.google.firebase.crashlytics'

android {
  compileSdkVersion 30

  defaultConfig {
    applicationId "com.jraska.github.client"
    minSdkVersion 24
    targetSdkVersion 30
    versionName '0.26.1'
    versionCode 96
    multiDexEnabled true

    testInstrumentationRunner "com.jraska.github.client.TestRunner"
  }

  adbOptions {
    installOptions "-g"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()

  signingConfigs {
    debug {
      storeFile file("debug.keystore")
      storePassword "github-client"
      keyAlias "github-client"
      keyPassword "github-client"
    }
    release {
      storeFile file("prod.keystore")
      storePassword = System.getenv('CLIENT_GITHUB_STORE_PASS')
      keyAlias "prod"
      keyPassword System.getenv('CLIENT_GITHUB_KEY_PASS')
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      applicationIdSuffix '.dev'
      versionNameSuffix '-dev'
    }

    release {
      signingConfig signingConfigs.release
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  packagingOptions {
    exclude 'META-INF/licenses/ASM'
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  implementation project(':core-api')
  implementation project(':core')
  implementation project(':core-android-api')
  implementation project(':navigation-api')
  implementation project(':feature:navigation-deeplink')
  implementation project(':feature:identity')
  implementation project(':feature:identity-api')
  implementation project(':feature:network-status')
  implementation project(':feature:push')
  implementation project(':feature:users')
  implementation project(':feature:repo')
  implementation project(':feature:settings')
  implementation project(':feature:about')
  implementation project(':feature:shortcuts')
  implementation project(':feature:chrome-custom-tabs')
  implementation project(':feature:in-app-update')
  implementation project(':feature:config-debug')
  implementation project(':feature:config-debug-api')

  implementation 'androidx.appcompat:appcompat:1.2.0'
  implementation 'androidx.legacy:legacy-support-v4:1.0.0'
  implementation 'com.google.android.material:material:1.3.0'
  implementation 'androidx.cardview:cardview:1.0.0'
  implementation 'androidx.core:core:1.3.2'
  implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
  implementation 'androidx.media:media:1.2.1'

  implementation platform('com.google.firebase:firebase-bom:26.8.0')
  implementation 'com.google.firebase:firebase-core'
  implementation 'com.google.firebase:firebase-config'
  implementation 'com.google.firebase:firebase-perf'
  implementation 'com.google.firebase:firebase-crashlytics'
  implementation 'com.google.firebase:firebase-messaging'
  implementation 'com.google.firebase:firebase-database'

  implementation 'androidx.lifecycle:lifecycle-runtime:2.3.1'

  implementation rootProject.ext.fresco

  implementation 'com.airbnb.android:epoxy:4.4.3'

  kapt rootProject.ext.daggerAnnotationProcessor
  implementation rootProject.ext.dagger

  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
  implementation 'com.jakewharton.threetenabp:threetenabp:1.3.0'
  implementation rootProject.ext.okHttpLoggingInterceptor

  testImplementation 'com.google.code.gson:gson:2.8.6'

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'javax.inject:javax.inject:1'
  testImplementation 'org.mockito:mockito-core:3.8.0'
  testImplementation 'org.threeten:threetenbp:1.5.0' // to fix failures with three ten db

  androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
  androidTestImplementation 'androidx.test:runner:1.3.0'
  androidTestImplementation 'androidx.test:rules:1.3.0'
  androidTestImplementation 'org.assertj:assertj-core:3.19.0'
  androidTestImplementation 'com.airbnb.okreplay:okreplay:1.6.0'
  androidTestImplementation 'com.airbnb.okreplay:espresso:1.6.0'
  androidTestImplementation 'com.squareup.rx.idler:rx2-idler:0.11.0'
  androidTestImplementation project(':core-testing')
  kaptAndroidTest rootProject.ext.daggerAnnotationProcessor
}

apply plugin: 'com.jraska.module.graph.assertion'

moduleGraphAssert {
  maxHeight = 2
  moduleLayers = ["[\\S:]*(?<!-api)", "[\\S:]*-api"]
  moduleLayersExclude = [":app -> [\\S:]*"]
  restricted = [":feature:\\S* -X> :not-wanted-module-example"]
}

apply plugin: 'com.google.gms.google-services'

play {
  track = 'internal'
  serviceAccountCredentials = file("$buildDir/play-publish-key.json")
}

project.afterEvaluate {
  if (gradle.startParameter.taskNames.any{ it.startsWith("publishBundle")}) {
    def credentials = System.getenv("PLAY_PUBLISHER_CREDENTIALS")
    if (credentials == null) {
      throw new GradleException("PLAY_PUBLISHER_CREDENTIALS not set, cannot release")
    }
    buildDir.mkdir()
    new File("$buildDir/play-publish-key.json").write(credentials)
  }
}
