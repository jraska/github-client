buildscript {
  ext.kotlin_version = '1.3.61'
  repositories {
    google()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url "https://plugins.gradle.org/m2/" }
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.5.3'
    classpath 'com.google.gms:google-services:4.3.3'
    classpath 'com.google.firebase:firebase-plugins:2.0.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath 'io.fabric.tools:gradle:1.29.0'
    classpath 'gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:6.1.0'
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

subprojects {
  apply plugin: "org.jlleitschuh.gradle.ktlint"
  project.plugins.whenPluginAdded { plugin ->
    if ('com.android.build.gradle.AppPlugin' == plugin.class.name
      || 'com.android.build.gradle.LibraryPlugin' == plugin.class.name) {

      project.android.testOptions.unitTests.all {
        testLogging {
          events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
          exceptionFormat 'full'
        }
      }
    }
  }
}


subprojects { subProject ->
  configurations.all { configuration ->
    // https://github.com/square/okio/issues/647
    // https://youtrack.jetbrains.com/issue/KT-31641
    // https://youtrack.jetbrains.com/issue/KT-33206
    if (name.contains('kapt')) {
      attributes.attribute(Usage.USAGE_ATTRIBUTE, subProject.objects.named(Usage.class, Usage.JAVA_RUNTIME))
    }
  }
}
