// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext.kotlin_version = '1.2.71'
  repositories {
    google()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url "https://plugins.gradle.org/m2/" }
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.2.1'
    classpath 'com.google.gms:google-services:4.0.1'
    classpath 'com.google.firebase:firebase-plugins:1.1.4'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath 'io.fabric.tools:gradle:1.26.0'
    classpath 'com.airbnb.okreplay:gradle-plugin:1.4.0'
    classpath 'gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:6.1.0'
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

subprojects {
  apply plugin: "org.jlleitschuh.gradle.ktlint"
  project.plugins.whenPluginAdded { plugin ->
    if ('com.android.build.gradle.AppPlugin' == plugin.class.name
      || 'com.android.build.gradle.LibraryPlugin' == plugin.class.name) {

      project.android.testOptions.unitTests.all {
        testLogging {
          events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
          exceptionFormat 'full'
        }
      }
    }
  }
}
