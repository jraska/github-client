// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext.kotlin_version = '1.1.51'
  repositories {
    maven { url 'https://maven.google.com' }
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.0.0'
    classpath 'com.google.gms:google-services:3.1.1'
    classpath 'com.google.firebase:firebase-plugins:1.1.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

allprojects {
  repositories {
    jcenter()
    maven { url 'https://maven.google.com' }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

ext {
  version = '0.6.0'
  versionCode = 6
  minSdkVersion = 21
  compileSdkVersion = 26
  targetSdkVersion = compileSdkVersion
  buildToolsVersion = '26.0.2'
  sourceCompatibility = JavaVersion.VERSION_1_8
}

subprojects {

  project.plugins.whenPluginAdded { plugin ->
    if ('com.android.build.gradle.AppPlugin'.equals(plugin.class.name)
        || 'com.android.build.gradle.LibraryPlugin'.equals(plugin.class.name)) {

      project.android.testOptions.unitTests.all {
        testLogging {
          events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
          exceptionFormat 'full'
        }
      }
    }
  }
}
