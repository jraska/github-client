buildscript {
  ext.kotlin_version = '1.5.0'
  repositories {
    google()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url "https://plugins.gradle.org/m2/" }
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:4.2.0'
    classpath('com.google.firebase:firebase-plugins:2.0.0') {
      exclude group: 'com.google.api-client', module: 'google-api-client' // conflicts with com.github.triplet.play
    }
    classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.2'
    classpath 'com.google.gms:google-services:4.3.5'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

subprojects {
  project.plugins.whenPluginAdded { plugin ->
    if ('com.android.build.gradle.AppPlugin' == plugin.class.name
      || 'com.android.build.gradle.LibraryPlugin' == plugin.class.name) {

      project.android.testOptions.unitTests.all {
        testLogging {
          events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
          exceptionFormat 'full'
        }
      }
    }
  }
}

subprojects {
  if (!gradle.startParameter.taskNames.any { it.contains("lint") }) {
    tasks.configureEach {
      if (name.startsWith("lint")) {
        enabled = false
      }
    }
  }
}

project.rootProject.subprojects.find { it.name == "core" }.afterEvaluate {
  it.tasks.named("check").configure {
    it.dependsOn(project.gradle.includedBuild("plugins").task(":test"))
  }
}

apply from: 'dependencies.gradle'
