buildscript {
  ext.kotlin_version = '1.4.0'
  repositories {
    google()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url "https://plugins.gradle.org/m2/" }
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:4.0.1'
    classpath ('com.google.firebase:firebase-plugins:2.0.0') {
      exclude group: 'com.google.api-client', module: 'google-api-client' // conflicts with com.github.triplet.play
    }
    classpath 'com.google.gms:google-services:4.3.3'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath 'io.fabric.tools:gradle:1.31.2'
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

subprojects {
  project.plugins.whenPluginAdded { plugin ->
    if ('com.android.build.gradle.AppPlugin' == plugin.class.name
      || 'com.android.build.gradle.LibraryPlugin' == plugin.class.name) {

      project.android.testOptions.unitTests.all {
        testLogging {
          events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
          exceptionFormat 'full'
        }
      }
    }
  }
}

subprojects {
  tasks.configureEach {
    if (name.startsWith("lint")) {
      enabled = false
    }
  }
}

ext {
  daggerAnnotationProcessor = 'com.google.dagger:dagger-compiler:2.28.3'
  dagger = 'com.google.dagger:dagger:2.27'

  okHttpVersion = '4.6.0'
  okHttp = "com.squareup.okhttp3:okhttp:$okHttpVersion"
  okHttpLoggingInterceptor = "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

  retrofitVersion = '2.8.1'
  retrofit = "com.squareup.retrofit2:retrofit:$retrofitVersion"
  retrofitGsonConverter = "com.squareup.retrofit2:converter-gson:$retrofitVersion"
  retrofitRxJavaAdapter = "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
}
